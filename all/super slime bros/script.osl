window.framerate = 60
github = "https://superslimebros.mistium.com/"

def "switchslime" "slime, player" (
  switch slime (
    case "basic"
      out_speed = 4
      out_knockback = 1
      out_weight = 1.75
      out_size = 1
      break
    case "heavy"
      out_speed = 3
      out_knockback = 1.5
      out_weight = 2
      out_size = 1.5
      break
    case "light"
      out_speed = 6
      out_knockback = 0.35
      out_weight = 1.5
      out_size = 1
      break
  )
  if player == 1 (
    player1_type = slime
    player1_speed = out_speed
    player1_knockback = out_knockback
    player1_gravity = out_weight
    player1_size = out_size * 200
  )
  if player == 2 (
    player2_type = slime
    player2_speed = out_speed
    player2_knockback = out_knockback
    player2_gravity = out_weight
    player2_size = out_size * 200
  )
)

def "switchability" "abi, pl" (
  if pl == 1 (
    player1_ability = abi
    player1_ability_lastused = 0
  )
  if pl == 2 (
    player2_ability = abi
    player2_ability_lastused = 0
  )
)

def addPlayer(x,y) (
  players.append({
    x,
    y,
    xvel: 0,
    yvel: 0,
    health: 0,
    lives: 3,
    ifrm: 0,
    anim: "idle",
    frame: 1,
    endfrm: 3,
    stretch: -100,
    target: -100,
    grounded: true,
    dir: "l",
    double: false
  })
)

def "reset_data" (
  array players = []

  number guy1_frm = 0
  number guy1_stretch = 80
  number guy1_direction = 90

  number guy2_frm = 0
  number guy2_stretch = -80
  number guy2_direction = 90

  // player main data
  number player1_x = -100
  number player1_y = 50
  number player1_xvel = 0
  number player1_yvel = 0
  number player1_health = 0
  number player1_lives = 3
  number player1_ifrm = 0

  number player2_x = 100
  number player2_y = 50
  number player2_xvel = 0
  number player2_yvel = 0
  number player2_health = 0
  number player2_lives = 3
  number player2_ifrm = 0

  // player animation variables
  string player1_anim = "idle"
  number player1_frame = 1
  number player1_endfrm = 3

  string player2_anim = "idle"
  number player2_frame = 1
  number player2_endfrm = 3

  // player display variables
  number player1_stretch = -100
  number player1_target = -100
  
  boolean player1_grounded = true
  string player1_dir = "r"
  boolean player1_double = false

  number player2_stretch = 100
  number player2_target = 100
  
  boolean player2_grounded = true
  string player2_dir = "l"
  boolean player2_double = false

  number shake_x = 0
  number shake_y = 0
  number shake_x_mag = 0
  number shake_y_mag = 0

  number p1_lst = timer
  number p2_lst = timer

  number cam_x = 0
  number cam_y = 0
  number stage_x = 0
  number stage_y = 0

  number width = 200
  number height = 200
  number logo_dir = 90
)

zoom = 1
main_image = random(0,3)

object types = {
  "basic":"The Basic Slime :/",
  "heavy":"Slower, heavier, less knockback",
  "light":"Faster, lighter, more knockback"
}

object abilities = {
  "heal":"Gain Back Your Health Over Time",
  "double_jump":"Jump While Not Touching The Ground",
  "dash":"Go Really Fast In The Air"
}

array stages = [
  "Spike",
  "Tree",
  "Mushroom"
]

object stage_offset = {
  "spike":0,
  "tree":60,
  "mushroom":105
}

curlast = 0
curframe = 0

switchslime "basic" 1
switchslime "basic" 2
switchability "dash" 1
switchability "dash" 2

window "dimensions" 1200 600
window "show"

stage = 0
selected_stage = 0
page = "main"
window_colour = #fff

def "anim_guys" (
  guy1_frm = random(4, 8)
  guy1_stretch = random(90, 110)
  guy1_direction = random(80, 100)
  guy2_frm = random(0, 5)
  guy2_stretch = random(-80, -120)
  guy2_direction = random(70, 110)
)

def "p1_attack" (
  if timer - p2_ifrm_temp > 0.2 (
    player2_health += 5
    player2_yvel += player2_health + 30 / player2_knockback / 10
    player2_y += 10
    anim_guys
    
    if player1_dir == "r" (
      player2_xvel += player2_health + 60 / player2_knockback
    )
    if player1_dir == "l" (
      player2_xvel -= player2_health + 60 / player2_knockback
    )
    
    zoom = 0.95
    player2_ifrm = timer
  )
  pause = timer + 0.1
)

def "p2_attack" (
  if timer - p1_ifrm_temp > 0.2 (
    player1_health += 5
    player1_yvel += player1_health + 30 / player1_knockback / 10
    player1_y += 10
    anim_guys
    
    if player2_dir == "r" (
      player1_xvel += player1_health + 60 / player1_knockback
    )
    if player2_dir == "l" (
      player1_xvel -= player1_health + 60 / player1_knockback
    )
    
    zoom = 0.95
    player1_ifrm = timer
  )
  pause = timer + 0.1
)

def "attack" "player, attack_type" (
  if player == 1 and attack_type == 1 (
    if timer - p1_ifrm_temp > 0.5 (
      goto player1_x player1_y + player1_yvel
      if player1_dir == "r" "change 40" else "change -40"
      hitbox 200 150 player2_x + cam_x player2_y + cam_y
      if collided "p1_attack"
    )
  )
  if player == 2 and attack_type == 1 (
    if timer - p2_ifrm_temp > 0.5 (
      goto player2_x player2_y + player2_yvel
      if player2_dir == "r" "change 40" else "change -40"
      hitbox 200 150 player1_x + cam_x player1_y + cam_y
      if collided "p2_attack"
    )
  )
)

def "collide" "width, height, reset, droppable" (
  hitbox width height player1_x + cam_x player1_y - 40 + cam_y
  if p1_upordown and droppable or droppable.not() and collided (
    player1_grounded = true
    player1_y = reset
    player1_yvel = 0
  )

  hitbox width height player2_x + cam_x player2_y - 40 + cam_y
  if p2_upordown and droppable or droppable.not() and collided (
    player2_grounded = true
    player2_y = reset
    player2_yvel = 0
  )
)

def "ssb_button" "txt, location" (
  change mouse_x - x_position / 40 mouse_y - y_position / 40
  square 360 40 10 : c#fff
  if mouse_touching (
    if onclick (
      page = location
    )
    change mouse_x - x_position / 20 mouse_y - y_position / 20
    square 360 40 20 : c#eee
  )
  text txt 10 : c#000 chx#-170
)

def "slime" "sli_name, pl" (
  if selected == sli_name (
    square 80 80 15 : c#000
  )
  square 80 80 10 : c#eee
  if onclick "switchslime sli_name pl"
  image github ++ "assets/" ++ sli_name ++ "/walk/" ++ curframe.round ++ ".png" 130
)

def "ability" "abi_name, pl" (
  if selected == abi_name (
    square 80 80 15 : c#000
  )
  square 80 80 10 : c#eee
  if onclick "switchability abi_name pl"
  image github ++ "assets/abilities/" ++ abi_name ++ ".png" 80
)

def "screenshake" "x, y" (
  shake_x_mag += x * 10
  shake_y_mag += y * 10
)

def "load" "link" (
  // loads an image
  link = github ++ link ++ ".png"
  loading.append(link)
  loading_total += 1
)

def "update_stage_data" (
  current_level_view = stages[lvl_page]
  name = current_level_view.toLower()
  url = github ++ "assets/stages/" ++ name ++ ".png"
  
  selected_stage = url
  stage_name = name
)

reset_data

loading_total = 0
array loading = []

for count 6 (
  load "assets/basic/walk/" ++ count
  load "assets/heavy/walk/" ++ count
  load "assets/light/walk/" ++ count
)
for count 3 (
  load "assets/basic/idle/" ++ count
  load "assets/heavy/idle/" ++ count
  load "assets/light/idle/" ++ count
)
for count 7 (
  load "assets/basic/attack1/" ++ count
  load "assets/heavy/attack1/" ++ count
  load "assets/light/attack1/" ++ count
)
for count 9 (
  load "assets/guy/" ++ (count - 1)
)
load "assets/abilities/dash"
load "assets/abilities/double_jump"
load "assets/abilities/heal"

load "assets/ui/controls"

load "assets/stages/mushroom"
load "assets/stages/spike"
load "assets/stages/tree"

string current_level_view = "Spike"
tot_lvls = 3
selected_stage = github ++ "assets/stages/spike.png"
string stage_name = "spike"

loading_cur = "Loading"
cur_loaded_image = 0
array loaded_images = []

mainloop:

if loading.len > 0 (
  // handle loading the assets
  if loading[1].imageinfo("loaded") (
    loading.delete(1)
  ) else (
    loading_cur = loading[1]
    image loading_cur 0 0
  )
  load_percent = 1 - (loading.len / loading_total)
  // calculates the percentage of images loaded

  c #333
  goto 0 0
  bar 300 20 10 load_percent
  goto -130 -30
  text (load_percent * 100).round() ++ "% Loaded" 10 : c#000
  loc 2 2 20 -20
  text `Loading: ${loading_cur}` 10 : c#000
  // renders a loading bar for the currently loading images

  loc 2 -2 100 30
  square 170 30 10 : c#ccc
  if clicked "window stop"

  loc 2 -2 30 30
  icon "close" 0.7 : c#000
  text "Close Game" 10 : chx#20

  exit
)

cur_loaded_image += 1
if cur_loaded_image > loaded_images.len "cur_loaded_image = 1"
image loaded_images[cur_loaded_image] 0

zoom += 1 - zoom / 20
if page == "win" (
  loc 2 2 25 -25
  square 30 30 15 : c#277564
  icon "home" 1 : c#fff
  if mouse_touching (
    cursor "pointer"
    if clicked (
      stage = 0
      page = "main"
    )
  )
  assets = "https://raw.githubusercontent.com/Mistium/Super-Slime-Bros/main/assets/"
  if player1_lives > 0 (
    player_image = assets ++ player1_type ++ "/walk/1.png"
    txt = "Player 1 Wins!"
  ) else (
    player_image = assets ++ player2_type ++ "/walk/1.png"
    txt = "Player 2 Wins!"
  )
  t = timer * 150
  width += 100 - (t.sin() * 5) * 10 - width / 5
  height += 100 + (t.sin() * 5) * 10 - height / 5
  logo_dir += 90 - logo_dir / 5
  goto txt.len * -10 height * -0.4 + 210
  text txt 20 : c#000
  
  goto 0 110
  direction logo_dir
  image player_image width height
  if onclick (
    width = 200
    height = 200
    logo_dir = random(80,100)
  )
  exit
)
if stage == 0 and page == "main" (
  loc -5 -5 mouse_x / 10 mouse_y / 10
  image github ++ "assets/stages/spike.png" window_width + 700 / 2
)
  if stage == 0 (
    loc 2 999 210 0
    change mouse_x - x_position / 80 mouse_y - y_position / 80
    square 400 window_height - 30 10 : c#ddd
    switch page (
      case "main"
        loc 2 2 mouse_x - x_position / 60 + 30 mouse_y - y_position / 60 - 50
        text "Super Slime Bros" 20 : c#000
  
        loc 2 2 210 -120
        ssb_button "Start Battle" "level_select"
        lvl_page = 1
        loc 2 2 210 -180
        ssb_button "Fullscreen Window" "fullscreen"
        loc 2 2 210 -240
        ssb_button "Quit Game" "quit"
  
        loc -2 2 mouse_x - x_position / 50 - 170 mouse_y - y_position / 50 - 170
        square 300 300 10 : c#ddd
        image github ++ "assets/ui/controls.png" 320
        break
      case "level_select"
        loc 2 2 mouse_x - x_position / 60 + 30 mouse_y - y_position / 60 - 50
        text "Level Select" 20 : c#000
        loc 2 -2 210 120
        ssb_button Play play
        loc 2 -2 210 60
        ssb_button "Back to Main Menu" "main"
  
        if timer - 0.15 > curlast (
          curframe += 1
          curlast = timer
        )
        if curframe > 6 "curframe = 1"
  
        c #888
        loc 2 2 mouse_x - x_position / 50 mouse_y - y_position / 50 - 80
        x = x_position
        y = y_position
        loc 2 -2 mouse_x - x_position / 50 + 400 mouse_y - y_position / 50 + 180
        frame x y x_position y_position 770
  
        loc 2 2 190 -200 + scroll_y
        square 280 340 50 : c#8a3d34
        square 280 340 40 : c#fff
        change -150 160
        text "Player 1" 13 : c#000
        loc 2 2 40 -70 + scroll_y
        text "Slime Type" 10
        loc 2 2 190 -140 + scroll_y
        square 280 80 25 : c#ddd
        selected = player1_type
        slime "basic" 1 : chx#-100
        slime "light" 1 : chx#100
        slime "heavy" 1 : chx#100
        loc 2 2 40 -210 + scroll_y
        text types[selected] 8 : c#000
  
        loc 2 2 40 -240 + scroll_y
        text "Slime Ability" 10
        loc 2 2 190 -310 + scroll_y
        square 280 80 25 : c#ddd
        selected = player1_ability
        ability "dash" 1 : chx#-100
        ability "heal" 1 : chx#100
        ability "double_jump" 1 : chx#100
        loc 2 2 40 -375 + scroll_y
        text abilities[player1_ability] 8 : c#000
  
        loc 2 2 190 -600 + scroll_y
        square 280 340 50 : c#345b8a
        square 280 340 40 : c#fff
        change -150 160
        text "Player 2" 13 : c#000
        loc 2 2 40 -470 + scroll_y
        text "Slime Type" 10
        loc 2 2 190 -540 + scroll_y
        square 280 80 25 : c#ddd
        selected = player2_type
        slime "basic" 2 : chx#-100
        slime "light" 2 : chx#100
        slime "heavy" 2 : chx#100
        loc 2 2 40 -615 + scroll_y
        text types[selected] 8 : c#000
  
        loc 2 2 40 -640 + scroll_y
        text "Slime Ability" 10
        loc 2 2 190 -710 + scroll_y
        square 280 80 25 : c#ddd
        selected = player2_ability
        ability "dash" 2 : chx#-100
        ability "heal" 2 : chx#100
        ability "double_jump" 2 : chx#100
        loc 2 2 40 -775 + scroll_y
        text abilities[player2_ability] 8 : c#000
  
        frame "clear"
        loc 999 -2 200 40
        text `${lvl_page}/${tot_lvls} (${current_level_view})` 10 : c#000
        loc 999 999 220 30
        w = selected_stage.imageinfo("width") * (window_width - 450 - 10 / selected_stage.imageinfo("width")) - 30
        h = window_height - 100
        square w h 15 : c#000
        square w h 10 : c#ddd
        frame x_position - (w / 2) y_position + (h / 2) x_position + (w / 2) y_position - (h / 2)
        image selected_stage frame_width - 40
        frame "clear"
        loc -2 -2 -40 40
        square 30 30 15 : c#000
        square 30 30 10 : c#ddd
        icon "right-arrow" 1 : c#000
        if mouse_touching (
          cursor "pointer"
          if onclick (
            lvl_page += 1
            if lvl_page > tot_lvls "lvl_page = 1"
            update_stage_data
          )
        )
        loc 2 -2 480 40
        square 30 30 15 : c#000
        square 30 30 10 : c#ddd
        icon "left-arrow" 1 : c#000
        if mouse_touching (
          cursor "pointer"
          if onclick (
            lvl_page -= 1
            if lvl_page < 1 "lvl_page = tot_lvls"
            update_stage_data
          )
        )
        break
      case "play"
        page = 0
        stage = selected_stage
        reset_data
        break
      case "fullscreen"
        window "maximise"
        page = "main"
        break
      case "quit"
        window "stop"
        break
    )
  )
  if stage != 0 (
    p1_upordown = "s".isKeyDown().not() and "w".isKeyDown().not()
    p2_upordown = "arrowDown".isKeyDown().not() and "arrowUp".isKeyDown().not()
    shake_x += shake_x_mag * delta_time
    shake_y += shake_y_mag * delta_time
    shake_x_mag -= shake_x_mag * 1.5
    shake_y_mag -= shake_y_mag * 1.5
    cam_x += player1_x + player2_x / -2 - cam_x * delta_time * 4
    cam_y += player1_y + player2_y / -2 - cam_y * delta_time * 4

    frame window.left * 2 + cam_x window.top * 2 + cam_y window.right * 2 + cam_x window.bottom * 2 + cam_y
    
    guy1_stretch += 100 - guy1_stretch / 10
    guy1_direction += 90 - guy1_direction / 10
    guy2_stretch += -100 - guy2_stretch / 10
    guy2_direction += 90 - guy2_direction / 10

    stage_x = shake_x
    stage_y = shake_y
    
    goto stage_x / 5 - (window_width / 3) stage_y / 5
    direction guy1_direction
    stretch "x" guy1_stretch
    image github ++ "assets/guy/" ++ guy1_frm ++ ".png" window_width / 2

    goto stage_x / 5 + (window_width / 3) stage_y / 5
    direction guy2_direction
    stretch "x" guy2_stretch
    image github ++ "assets/guy/" ++ guy2_frm ++ ".png" window_width / 2

    direction 90
    stretch "x" 100
    goto stage_x stage_y
    change_y stage_offset[stage_name]
    image stage 1000 * zoom

    if player1_anim != "attack1" (
      move = "d".isKeyDown() or "a".isKeyDown()
      if player1_anim != "walk" and move (
        player1_anim = "walk"
        player1_frame = 1
        player1_endfrm = 6
      ) else (
        if player1_anim != "idle" (
          player1_anim = "idle"
          player1_frame = 1
          player1_endfrm = 3
        )
      )
      if "space".onKeyDown() (
        player1_anim = "attack1"
        player1_frame = 1
        player1_endfrm = 7
      )
    )

    if player2_anim != "attack1" (
      move = "arrowRight".isKeyDown() or "arrowLeft".isKeyDown()
      if player2_anim != "walk" and move (
        player2_anim = "walk"
        player2_frame = 1
        player2_endfrm = 6
      ) else (
        if player2_anim != "idle" (
          player2_anim = "idle"
          player2_frame = 1
          player2_endfrm = 3
        )
      )
      if "enter".onKeyDown() (
        player2_anim = "attack1"
        player2_frame = 1
        player2_endfrm = 7
      )
    )

    if player1_dir == "r" "player1_target = 100" else "player1_target = -100"
    if player2_dir == "r" "player2_target = 100" else "player2_target = -100"

    player1_speedmod = player1_speed
    if player1_grounded "player1_speedmod += 2"
    if player1_ability == "dash" and player1_grounded.not() "player1_speedmod *= 2"
    if timer - player1_ifrm < 0.5 "player1_speedmod /= 4"

    player2_speedmod = player2_speed
    if player2_grounded "player2_speedmod += 2"
    if player2_ability == "dash" and player2_grounded.not() "player2_speedmod *= 2"
    if timer - player2_ifrm < 0.5 "player2_speedmod /= 4"

    if player1_ability == "heal" "player1_health -= 0.02"
    if player2_ability == "heal" "player2_health -= 0.02"

    player1_health.clamp(0,9999)
    player2_health.clamp(0,9999)

    if "d".isKeyDown() (
      player1_xvel += player1_speedmod
      player1_dir = "r"
    )
    if "a".isKeyDown() (
      player1_xvel -= player1_speedmod
      player1_dir = "l"
    )
    if player1_ability == "double_jump" and player1_double and "w".isKeyDown() and player1_grounded.not() (
      player1_yvel = 50
      player1_stretch *= 0.3
      player1_target *= 0.1
      player1_double = false
    )
    if "w".isKeyDown() and player1_grounded (
      player1_yvel = 50
      player1_grounded = false
      player1_stretch *= 0.3
      player1_target *= 0.1
      player1_double = true
    )
  
    if "arrowRight".isKeyDown() (
      player2_xvel += player2_speedmod
      player2_dir = "r"
    )
    if "arrowLeft".isKeyDown() (
      player2_xvel -= player2_speedmod
      player2_dir = "l"
    )
    if player2_ability == "double_jump" and player2_double and "up arrow".isKeyDown() and player2_grounded.not() (
      player2_yvel = 50
      player2_stretch *= 0.3
      player2_target *= 0.1
      player2_double = false
    )
    if "arrowUp".isKeyDown() and player2_grounded (
      player2_yvel = 50
      player2_grounded = false
      player2_stretch *= 0.3
      player2_target *= 0.1
      player2_startdou = true
    )
    if player2_startdou and "arrowUp".isKeyDown().not (
      player2_double = true
    )
    player1_yvel -= player1_gravity
    player1_xvel *= 0.7
    player1_x += player1_xvel * 0.7
    player1_y += player1_yvel * 0.5

    player2_yvel -= player2_gravity
    player2_xvel *= 0.7
    player2_x += player2_xvel * 0.7
    player2_y += player2_yvel * 0.5

    player1_stretch += player1_target - player1_stretch * 0.1
    player2_stretch += player2_target - player2_stretch * 0.1

    if timer - 0.15 > p1_lst (
      player1_frame += 1
      p1_lst = timer
    )
    if timer - 0.15 > p2_lst (
      player2_frame += 1
      p2_lst = timer
    )
    p1_ifrm_temp = player1_ifrm
    p2_ifrm_temp = player2_ifrm
    if player1_anim == "attack1" (
      attack 1 1
    )
    if player2_anim == "attack1" (
      attack 2 1
    )

    if player1_frame > player1_endfrm (
      if player1_anim == "attack1" (
        player1_anim = "idle"
        player1_endfrm = 3
      )
      player1_frame = 1
    )
    if player2_frame > player2_endfrm (
      if player2_anim == "attack1" (
        player2_anim = "idle"
        player2_endfrm = 3
      )
      player2_frame = 1
    )

    if player1_y < -2000 (
      player1_x = 0
      player1_y = 500
      player1_xvel = 0
      player1_yvel = 0
      player1_lives -= 1
      player1_ifrm = timer + 3
      player1_health = 0
      zoom = 0.8
    )
    if player2_y < -2000 (
      player2_x = 0
      player2_y = 500
      player2_xvel = 0
      player2_yvel = 0
      player2_lives -= 1
      player2_ifrm = timer + 3
      player2_health = 0
      zoom = 0.8
    )

    if player1_lives < 1 (
      page = "win"
    )
    if player2_lives < 1 (
      page = "win"
    )
    switch stage_name (
      case "spike"
        goto 0 -30
        collide 1000 60 30 false
        goto 250 165
        collide 200 40 210 true
        goto -320 195
        collide 200 40 240 true
        goto -20 330
        collide 160 40 380 true
        break
      case "tree"
        goto 0 -30
        collide 1000 60 30 false
        goto 350 120
        collide 100 40 180 true
        goto -320 195
        collide 200 40 240 true
        goto -20 330
        collide 160 40 380 true
        break
      case "mushroom"
        goto 0 -30
        collide 1000 60 30 false
        goto -260 80
        collide 80 40 130 true
        goto -340 290
        collide 190 40 350 true
        goto -160 240
        collide 110 40 290 true
        goto -60 260
        collide 110 60 310 true
        goto 30 280
        collide 110 60 350 true
        goto 300 380
        collide 190 40 430 true
        break
    )

    z = 20 * zoom
    goto player1_x * zoom player1_y * zoom + 20
    if player1_type == "heavy" "change_y 25 * zoom"
    stretch "x" player1_stretch.int
    image `https://raw.githubusercontent.com/Mistium/Super-Slime-Bros/main/assets/${player1_type}/${player1_anim}/${player1_frame.round}.png` player1_size * zoom
    change_y 60 * zoom
    square z z 10 * zoom : c#8a3d34
    change_x -5 * zoom
    text "1" 10 * zoom : c#fff

    goto player2_x * zoom player2_y * zoom + 20
    if player2_type == "heavy" "change_y 25 * zoom"
    stretch "x" player2_stretch.toNum()
    image `https://raw.githubusercontent.com/Mistium/Super-Slime-Bros/main/assets/${player2_type}/${player2_anim}/${player2_frame.round}.png` player2_size * zoom
    change_y 60 * zoom
    square z z 10 * zoom : c#345b8a
    change_x -5 * zoom
    text "2" 10 * zoom : c#fff
    
    frame "clear"

    loc 999 -2 -90 35
    square 150 50 20 : c#000
    square 150 50 10 : c#8a3d34

    loc 999 -2 90 35
    square 150 50 20 : c#000
    square 150 50 10 : c#345b8a

    loc 999 -2 90 100
    square 150 30 10 : c#333
    loc 999 -2 -90 100
    square 150 30 10 : c#333

    loc 999 -2 -160 35
    text `1: ${player1_health.round}%` 15 : c#fff

    loc 999 -2 20 35
    text `2: ${player2_health.round}%` 15

    loc 999 -2 -150 100
    text `${player1_lives} Lives Left` 10

    loc 999 -2 30 100
    text `${player2_lives} Lives Left` 10

    loc 2 2 25 -25
    square 30 30 15 : c#277564
    icon "home" 1 : c#fff
    if mouse_touching "cursor pointer"
    if clicked (
      stage = 0
      page = "main"
    )
  )
if mouse_down.not() "can = true"
frame "clear"